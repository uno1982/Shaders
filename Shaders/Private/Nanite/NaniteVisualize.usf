// Copyright Epic Games, Inc. All Rights Reserved.

#include "../Common.ush"
#include "../ShaderPrint.ush"
#include "../SceneData.ush"
#include "../DeferredShadingCommon.ush"
#include "../ColorMap.ush"
#include "../Visualization.ush"
#include "../VirtualShadowMaps/VirtualShadowMapPageCacheCommon.ush"
#include "../SplineMeshCommon.ush"
#include "../MeshPaintTextureCommon.ush"

#define NUM_VIRTUALTEXTURE_SAMPLES 1
#include "../VirtualTextureCommon.ush"

#include "NaniteDataDecode.ush"
#include "NaniteAttributeDecode.ush"
#include "NaniteCullingCommon.ush"
#include "NaniteEditorCommon.ush"

RWTexture2D<float4>		DebugOutput;

Texture2D<UlongType>	VisBuffer64;
Texture2D<UlongType>	DbgBuffer64;
Texture2D<uint>			DbgBuffer32;
Texture2D<float>		SceneDepth;
Texture2D<float>		SceneZDecoded;
Texture2D<uint4>		SceneZLayout;
Texture2D<uint>			ShadingMask;
Texture2D<uint>			FastClearTileVis;
Texture2D<float4>		MeshPaintTexture;

StructuredBuffer<FNaniteRasterBinMeta> RasterBinMeta;
ByteAddressBuffer		ShadingBinData;

int4					VisualizeConfig;
int4					VisualizeScales;
uint					RegularMaterialRasterBinCount;
int2					PickingPixelPos;
uint					MeshPaintTextureCoordinate;

// NOTE: Just hardcoded to ENaniteMeshPass::BasePass as an optimization, as all these shaders are only concerned with base pass materials
static const uint MeshPassIndex = 0;

RWStructuredBuffer<FNanitePickingFeedback> FeedbackBuffer;

// TODO: Try N.V to improve shading look
float3 ApplySobelFilter(uint2 PixelPosXY, uint DepthInt, float3 Color, float3 OutlineColor, bool bDarkOutline)
{
	// Sobel edge detect depth
	static int SobelX[] =
	{
		1, 0, -1,
		2, 0, -2,
		1, 0, -1
	};

	static int SobelY[] =
	{
		 1,  2,  1,
		 0,  0,  0,
		-1, -2, -1
	};

	static uint2 UVSample[] =
	{
		{-1,  1},  {0,  1},  {1,  1},
		{-1,  0},  {0,  0},  {1,  0},
		{-1, -1},  {0, -1},  {1, -1}
	};

	float3 DepthGradX = float3(0.0f, 0.0f, 0.0f);
	float3 DepthGradY = float3(0.0f, 0.0f, 0.0f);

	uint DepthIntCurrent;
	uint VisibleClusterIndexCurrent;
	uint TriIndexCurrent;

	for (uint Tap = 0; Tap < 9; ++Tap)
	{
		const UlongType VisPixelCurrent = VisBuffer64[PixelPosXY + UVSample[Tap]];
		UnpackVisPixel(VisPixelCurrent, DepthIntCurrent, VisibleClusterIndexCurrent, TriIndexCurrent);

		float SampleDensityDepth = log2( ConvertFromDeviceZ(asfloat(DepthIntCurrent)) + 1.0 ) * 10.0;

		DepthGradX += SobelX[Tap] * SampleDensityDepth;
		DepthGradY += SobelY[Tap] * SampleDensityDepth;
	}

	// Build outline from depth
	float3 DepthOutline = max(abs(DepthGradX), abs(DepthGradY));

	float3 CombineColor;
	if( bDarkOutline )
		CombineColor = Color * ( 1.0 - DepthOutline * 0.25 );
	else
		CombineColor = Color + DepthOutline * 0.25 * OutlineColor;

	return saturate(CombineColor);
}

uint GetVisualizeMode()
{
	return VisualizeConfig.x;
}

uint GetPickingDomain()
{
	return select(
		GetVisualizeMode() == NANITE_VISUALIZE_PICKING,
		VisualizeConfig.y,
		NANITE_PICKING_DOMAIN_TRIANGLE
	);
}

uint GetPixelProgrammableVisMode()
{
	return select(
		GetVisualizeMode() == NANITE_VISUALIZE_PIXEL_PROGRAMMABLE_RASTER,
		VisualizeConfig.y,
		NANITE_PIXEL_PROG_VIS_MODE_DEFAULT
	);
}

uint GetMeshPaintingShowMode()
{
	return select(
		GetVisualizeMode() == NANITE_VISUALIZE_VERTEX_COLOR || GetVisualizeMode() == NANITE_VISUALIZE_MESH_PAINT_TEXTURE,
		VisualizeConfig.y & 0x1,
		NANITE_MESH_PAINTING_SHOW_ALL
	);
}

uint GetMeshPaintingChannelMode()
{
	return select(
		GetVisualizeMode() == NANITE_VISUALIZE_VERTEX_COLOR || GetVisualizeMode() == NANITE_VISUALIZE_MESH_PAINT_TEXTURE,
		(VisualizeConfig.y >> 1) & 0x7,
		NANITE_MESH_PAINTING_CHANNELS_RGB
	);
}

uint GetMeshPaintingTextureMode()
{
	return select(
		GetVisualizeMode() == NANITE_VISUALIZE_MESH_PAINT_TEXTURE,
		(VisualizeConfig.y >> 4) & 0x1,
		NANITE_MESH_PAINTING_TEXTURE_DEFAULT
	);
}

float3 ApplyMeshPaintingChannelMode(uint Mode, float4 Color)
{
	switch (Mode)
	{
	case NANITE_MESH_PAINTING_CHANNELS_R:   return float3(Color.r, 0, 0);
	case NANITE_MESH_PAINTING_CHANNELS_G:   return float3(0, Color.g, 0);
	case NANITE_MESH_PAINTING_CHANNELS_B:   return float3(0, 0, Color.b);
	case NANITE_MESH_PAINTING_CHANNELS_A:   return Color.aaa;
	case NANITE_MESH_PAINTING_CHANNELS_RGB:	
	default:                                return Color.rgb;
	}
}

// Apply this to color output because we composite the visualization result with post-tone-mapped space.
// Call this for visualization modes where it matters, but possibly we should do this in all modes.
float3 LinearToPostTonemapSpace(float3 Color)
{
	return LinearToSrgbBranchless(Color);
}

float GetOverdrawScale()
{
	return clamp(float(VisualizeScales.x), 0.0f, 100.0f) / 100.0f;
}

float GetComplexityScale()
{
	return clamp(float(VisualizeScales.y), 0.0f, 100.0f) / 100.0f;
}

uint GetShadingExportCount()
{
	return uint(VisualizeScales.z);
}

bool GetCompositeWithSceneDepth()
{
	return VisualizeConfig.z != 0;
}

bool ShouldApplySobelFilter()
{
	return VisualizeConfig.w != 0;
}

[numthreads(8, 8, 1)]
void VisualizeCS(uint3 DTID : SV_DispatchThreadID, uint3 GID : SV_GroupID)
{
	const uint VisualizeMode = GetVisualizeMode();

	const uint2 PickingPos	= uint2(PickingPixelPos);
	const uint2 PixelPos	= DTID.xy + uint2(View.ViewRectMin.xy);
	const uint2 TilePos		= GID.xy; // 8x8 tile 2D coord

	const UlongType VisPixel = VisBuffer64[PixelPos];

	uint DepthInt;
	uint VisibleClusterIndex;
	uint TriIndex;
	bool bIsImposter;
	UnpackVisPixel(VisPixel, DepthInt, VisibleClusterIndex, TriIndex, bIsImposter);

	FNaniteView NaniteView = GetNaniteView( 0 );

	FShadingMask UnpackedMask = UnpackShadingMask(ShadingMask[PixelPos]);

	float3 Result = float3(0, 0, 0);
	float3 OutlineColor = 1;
	float Opacity = 1.0f;

	bool bDarkOutline = false;
	bool bApplySobel = true;

	if (VisualizeMode == NANITE_VISUALIZE_OVERDRAW)
	{
		uint DebugValueAdd = DbgBuffer32[PixelPos];

		const float OverdrawScale = GetOverdrawScale();
		const float OverdrawCount = DebugValueAdd; // Num of evaluations per pixel
		const float OverdrawColor = 1 - exp2(-OverdrawCount * OverdrawScale);
		Result = ColorMapInferno(OverdrawColor);
	}
	else if (VisibleClusterIndex != 0xFFFFFFFF && (!GetCompositeWithSceneDepth() || UnpackedMask.bIsNanitePixel))
	{
		// Nanite Pixel

		UlongType DbgPixel = DbgBuffer64[PixelPos];
		uint DebugDepthInt;
		uint DebugValueMax;
		UnpackDbgPixel(DbgPixel, DebugDepthInt, DebugValueMax);
		uint DebugValueAdd = DbgBuffer32[PixelPos];

		const uint PackedMaterialFlags = ShadingBinData.Load<FNaniteShadingBinMeta>(UnpackedMask.ShadingBin * NANITE_SHADING_BIN_META_BYTES).MaterialFlags;
		FNaniteMaterialFlags MaterialFlags = UnpackNaniteMaterialFlags(PackedMaterialFlags);

		FVisibleCluster VisibleCluster				= GetVisibleCluster(VisibleClusterIndex);
		FInstanceSceneData InstanceData				= GetInstanceSceneData(VisibleCluster, false);
		FInstanceDynamicData InstanceDynamicData	= CalculateInstanceDynamicData(NaniteView, InstanceData);
		FPrimitiveSceneData PrimitiveData			= GetPrimitiveData(InstanceData.PrimitiveId);
		FCluster Cluster							= GetCluster(VisibleCluster.PageIndex, VisibleCluster.ClusterIndex);

		const bool bIsSkinned = (PrimitiveData.Flags & PRIMITIVE_SCENE_DATA_FLAG_SKINNED_MESH) != 0 && Cluster.bSkinning;

		FNaniteSkinningHeader SkinningHeader = (FNaniteSkinningHeader)0;
		FBoneInfluenceHeader BoneInfluenceHeader = GetBoneInfluenceHeader(Cluster);
		BRANCH
		if (bIsSkinned)
		{
			SkinningHeader = LoadNaniteSkinningHeader(InstanceData.PrimitiveId);
		}

		const uint3 TriIndices = DecodeTriangleIndices(Cluster, TriIndex);

		float3 PointLocal0 = DecodePosition(TriIndices.x, Cluster);
		float3 PointLocal1 = DecodePosition(TriIndices.y, Cluster);
		float3 PointLocal2 = DecodePosition(TriIndices.z, Cluster);

		FNaniteRawAttributeData RawAttributeData[3];
		GetRawAttributeData3(RawAttributeData, Cluster, TriIndices, NANITE_MAX_UVS);

		FNaniteTangentBasis TangentBasis0 = MakeTangentBasis(RawAttributeData[0]);
		FNaniteTangentBasis TangentBasis1 = MakeTangentBasis(RawAttributeData[1]);
		FNaniteTangentBasis TangentBasis2 = MakeTangentBasis(RawAttributeData[2]);

		BRANCH
		if (bIsSkinned)
		{
			float3 SkinnedPosition[3];
			SkinnedPosition[0] = float3(0.0f, 0.0f, 0.0f);
			SkinnedPosition[1] = float3(0.0f, 0.0f, 0.0f);
			SkinnedPosition[2] = float3(0.0f, 0.0f, 0.0f);

			float3 SkinnedNormal[3];
			SkinnedNormal[0] = float3(0.0f, 0.0f, 0.0f);
			SkinnedNormal[1] = float3(0.0f, 0.0f, 0.0f);
			SkinnedNormal[2] = float3(0.0f, 0.0f, 0.0f);

			float3 SkinnedTangent[3];
			SkinnedTangent[0] = float3(0.0f, 0.0f, 0.0f);
			SkinnedTangent[1] = float3(0.0f, 0.0f, 0.0f);
			SkinnedTangent[2] = float3(0.0f, 0.0f, 0.0f);

			LOOP
			for (uint InfluenceIndex = 0; InfluenceIndex < BoneInfluenceHeader.NumVertexBoneInfluences; ++InfluenceIndex)
			{
				uint BoneIndex0 = 0;
				float BoneWeight0 = 0.0f;
				DecodeVertexBoneInfluence(BoneInfluenceHeader, TriIndices.x, InfluenceIndex, BoneIndex0, BoneWeight0);

				uint BoneIndex1 = 0;
				float BoneWeight1 = 0.0f;
				DecodeVertexBoneInfluence(BoneInfluenceHeader, TriIndices.y, InfluenceIndex, BoneIndex1, BoneWeight1);

				uint BoneIndex2 = 0;
				float BoneWeight2 = 0.0f;
				DecodeVertexBoneInfluence(BoneInfluenceHeader, TriIndices.z, InfluenceIndex, BoneIndex2, BoneWeight2);

				float4x3 CurrentBoneTransform[3];
				CurrentBoneTransform[0] = LoadNaniteBoneTransform(SkinningHeader.TransformBufferOffset + InstanceData.SkinningData + BoneIndex0);
				CurrentBoneTransform[1] = LoadNaniteBoneTransform(SkinningHeader.TransformBufferOffset + InstanceData.SkinningData + BoneIndex1);
				CurrentBoneTransform[2] = LoadNaniteBoneTransform(SkinningHeader.TransformBufferOffset + InstanceData.SkinningData + BoneIndex2);

				SkinnedPosition[0]	+= mul(float4(PointLocal0, 1.0f), CurrentBoneTransform[0]) * BoneWeight0;
				SkinnedPosition[1]	+= mul(float4(PointLocal1, 1.0f), CurrentBoneTransform[1]) * BoneWeight1;
				SkinnedPosition[2]	+= mul(float4(PointLocal2, 1.0f), CurrentBoneTransform[2]) * BoneWeight2;

				SkinnedNormal[0]	+= mul((float3x3)CurrentBoneTransform[0], TangentBasis0.TangentZ) * BoneWeight0;
				SkinnedNormal[1]	+= mul((float3x3)CurrentBoneTransform[1], TangentBasis1.TangentZ) * BoneWeight1;
				SkinnedNormal[2]	+= mul((float3x3)CurrentBoneTransform[2], TangentBasis2.TangentZ) * BoneWeight2;

				SkinnedTangent[0]	+= mul((float3x3)CurrentBoneTransform[0], TangentBasis0.TangentXAndSign.xyz) * BoneWeight0;
				SkinnedTangent[1]	+= mul((float3x3)CurrentBoneTransform[1], TangentBasis1.TangentXAndSign.xyz) * BoneWeight1;
				SkinnedTangent[2]	+= mul((float3x3)CurrentBoneTransform[2], TangentBasis2.TangentXAndSign.xyz) * BoneWeight2;
			}

			TangentBasis0.TangentZ = SkinnedNormal[0];
			TangentBasis1.TangentZ = SkinnedNormal[1];
			TangentBasis2.TangentZ = SkinnedNormal[2];

			TangentBasis0.TangentXAndSign.xyz = SkinnedTangent[0];
			TangentBasis1.TangentXAndSign.xyz = SkinnedTangent[1];
			TangentBasis2.TangentXAndSign.xyz = SkinnedTangent[2];

			PointLocal0 = SkinnedPosition[0];
			PointLocal1 = SkinnedPosition[1];
			PointLocal2 = SkinnedPosition[2];
		}

		const float3 PointWorld0 = mul(float4(PointLocal0, 1), InstanceDynamicData.LocalToTranslatedWorld).xyz;
		const float3 PointWorld1 = mul(float4(PointLocal1, 1), InstanceDynamicData.LocalToTranslatedWorld).xyz;
		const float3 PointWorld2 = mul(float4(PointLocal2, 1), InstanceDynamicData.LocalToTranslatedWorld).xyz;

		const float4 PointClip0 = mul(float4(PointWorld0, 1), NaniteView.TranslatedWorldToClip);
		const float4 PointClip1 = mul(float4(PointWorld1, 1), NaniteView.TranslatedWorldToClip);
		const float4 PointClip2 = mul(float4(PointWorld2, 1), NaniteView.TranslatedWorldToClip);

		const float2 PixelClip = (PixelPos + 0.5 - View.ViewRectMin.xy) * View.ViewSizeAndInvSize.zw * float2(2, -2) + float2(-1, 1);

		// Calculate perspective correct barycentric coordinates with screen derivatives
		const FBarycentrics Barycentrics = CalculateTriangleBarycentrics(PixelClip, PointClip0, PointClip1, PointClip2, View.ViewSizeAndInvSize.zw);

		const FNaniteAttributeData AttributeData = GetAttributeData(
			Cluster,
			PointLocal0,
			PointLocal1,
			PointLocal2,
			RawAttributeData[0],
			RawAttributeData[1],
			RawAttributeData[2],
			TangentBasis0,
			TangentBasis1,
			TangentBasis2,
			Barycentrics,
			InstanceData,
			NANITE_MAX_UVS
		);

		const bool bWPOEnabled = (VisibleCluster.Flags & NANITE_CULLING_FLAG_ENABLE_WPO) != 0;
		const bool bFallbackRaster = (VisibleCluster.Flags & NANITE_CULLING_FLAG_FALLBACK_RASTER) != 0;

		uint RasterBin = GetMaterialRasterBin(Cluster, InstanceData.PrimitiveId, MeshPassIndex, TriIndex, RegularMaterialRasterBinCount, bFallbackRaster);
		RasterBin = RemapRasterBin(RasterBin, RenderFlags, MaterialFlags);

		const int HierarchyOffset = InstanceData.NaniteHierarchyOffset;

		// Note: The mode is no longer a bitmask at this point, just a single visualization mode.

		if (VisualizeMode == NANITE_VISUALIZE_TESSELLATION)
		{
			const uint SubPatch = BitFieldExtractU32(DebugValueMax, 8,  8);
			const uint MicroTri = BitFieldExtractU32(DebugValueMax, 8, 16);

			const float3 PatchColor = IntToColor(TriIndex).x * 0.5 + 0.5;

			float3 SubPatchColor = IntToColor(SubPatch << 8u) * 0.6 + 0.4;
			float3 MicroTriColor = IntToColor(MicroTri) * 0.6 + 0.4;

			MicroTriColor = lerp(MicroTriColor, dot(MicroTriColor, float3(0.3, 0.6, 0.1)), 0.5);
			SubPatchColor /= max3(SubPatchColor.x, SubPatchColor.y, SubPatchColor.z);
			Result = MicroTriColor * SubPatchColor * PatchColor;
		}
		else if (VisualizeMode == NANITE_VISUALIZE_TRIANGLES)
		{
			const uint SubPatch = BitFieldExtractU32(DebugValueMax, 8,  8);
			const uint MicroTri = BitFieldExtractU32(DebugValueMax, 8, 16);
			if( MicroTri != 0u )
			{
				TriIndex = MurmurAdd( TriIndex, SubPatch );
				TriIndex = MurmurAdd( TriIndex, MicroTri );
			}

			Result = IntToColor( TriIndex );
			Result = Result * 0.8 + 0.2;
			bDarkOutline = true;
		}
		else if (VisualizeMode == NANITE_VISUALIZE_PATCHES)
		{
			Result = IntToColor(TriIndex);
			Result = Result * 0.8 + 0.2;
			bDarkOutline = true;
		}
		else if (VisualizeMode == NANITE_VISUALIZE_CLUSTERS)
		{
			Result = IntToColor(VisibleCluster.ClusterIndex);
			Result = Result * 0.8 + 0.2;
			bDarkOutline = true;
		}
		else if (VisualizeMode == NANITE_VISUALIZE_GROUPS)
		{
			Result = IntToColor(Cluster.GroupIndex);
		}
		else if (VisualizeMode == NANITE_VISUALIZE_PAGES)
		{
			Result = IntToColor(VisibleCluster.PageIndex);
		}
		else if (VisualizeMode == NANITE_VISUALIZE_PRIMITIVES)
		{
			Result = IntToColor(InstanceData.PrimitiveId) * 0.8;
		}
		else if (VisualizeMode == NANITE_VISUALIZE_INSTANCES)
		{
			Result = IntToColor(VisibleCluster.InstanceId) * 0.8;
		}
		else if (VisualizeMode == NANITE_VISUALIZE_RASTER_MODE)
		{
			const uint RasterMode = BitFieldExtractU32(DebugValueMax, 8,  0);
			uint VisValue = 0u;
			if (RasterMode == 1u)
			{
				// Hardware Raster
				VisValue = 1u;
			}
			else if (RasterMode == 2u)
			{
				// Software Raster
				VisValue = 2u;
			}
			else if (RasterMode == 3u)
			{
				// Software Tessellation
				VisValue = 6000u;
			}

			Result = (IntToColor(VisValue) * 0.75 + 0.25) * (IntToColor(0).x * 0.5 + 0.5);
		}
		else if (VisualizeMode == NANITE_VISUALIZE_RASTER_BINS)
		{
			Result = IntToColor(RasterBin) * 0.8 + 0.2;
		}
		else if (VisualizeMode == NANITE_VISUALIZE_SHADING_BINS)
		{
			const uint ShadingBin = GetMaterialShadingBin(Cluster, InstanceData.PrimitiveId, MeshPassIndex, TriIndex);
			Result = IntToColor(ShadingBin) * 0.8 + 0.2;
		}
		else if (VisualizeMode == NANITE_VISUALIZE_HIERARCHY_OFFSET)
		{
			Result = IntToColor(HierarchyOffset);
		}
		else if (VisualizeMode == NANITE_VISUALIZE_MATERIAL_COUNT)
		{
			Result = IntToColor(GetMaterialCount(Cluster));
		}
		else if (VisualizeMode == NANITE_VISUALIZE_MATERIAL_MODE)
		{
			Result = IsMaterialFastPath(Cluster) ? float3(0, 1, 0) : float3(1, 0, 0);
		}
		else if (VisualizeMode == NANITE_VISUALIZE_MATERIAL_INDEX)
		{
			Result = IntToColor(GetRelativeMaterialIndex(Cluster, TriIndex));
		}
		else if (VisualizeMode == NANITE_VISUALIZE_SHADING_WRITE_MASK)
		{
			const uint PackedFlags = ShadingBinData.Load<FNaniteShadingBinMeta>(UnpackedMask.ShadingBin * NANITE_SHADING_BIN_META_BYTES).MaterialFlags;
			const uint WriteMask = BitFieldExtractU32(PackedFlags, 8u, 24u);
			Result = IntToColor(WriteMask);
		}
		else if (VisualizeMode == NANITE_VISUALIZE_SCENE_Z_MIN || VisualizeMode == NANITE_VISUALIZE_SCENE_Z_MAX || VisualizeMode == NANITE_VISUALIZE_SCENE_Z_DELTA)// || VisualizeMode == NANITE_VISUALIZE_SCENE_Z_MASK)
		{
			const uint4 ZLayout = SceneZLayout[PixelPos];
			float2 MinMax = float2(f16tof32(ZLayout.y), f16tof32(ZLayout.y >> 16u));

			if (VisualizeMode == NANITE_VISUALIZE_SCENE_Z_MIN)
			{
				Result = float3(pow(MinMax.x, 0.11f), 0.0f, 0.0f);
			}
			else if (VisualizeMode == NANITE_VISUALIZE_SCENE_Z_MAX)
			{
				Result = float3(pow(MinMax.y, 0.11f), 0.0f, 0.0f);
			}
			else if (VisualizeMode == NANITE_VISUALIZE_SCENE_Z_DELTA)
			{
				Result = float3(pow(MinMax.y - MinMax.x, 0.11f), 0.0f, 0.0f);
			}
			//else if (VisualizeMode == NANITE_VISUALIZE_SCENE_Z_MASK)
			//{
			//	Result = IntToColor((uint)ZLayout.z);
			//}

			bApplySobel = false;
		}
		else if (VisualizeMode == NANITE_VISUALIZE_SCENE_Z_DECODED)
		{
			const float ZDecoded = SceneZDecoded[PixelPos];
			Result = float3(pow(ZDecoded, 0.11f), 0.0f, 0.0f);
			bApplySobel = false;
		}
	#if USE_EDITOR_SHADERS
		else if (VisualizeMode == NANITE_VISUALIZE_HIT_PROXY_DEPTH)
		{
			if ((InstanceData.Flags & INSTANCE_SCENE_DATA_FLAG_HAS_EDITOR_DATA) != 0u)
			{
				Result = IntToColor(InstanceData.EditorData.HitProxyPacked);
			}
			else
			{
				Result = IntToColor(GetMaterialHitProxyId(Cluster, InstanceData.PrimitiveId, TriIndex, MaterialHitProxyTable));
			}
		}
	#endif
		else if (VisualizeMode == NANITE_VISUALIZE_NO_DERIVATIVE_OPS)
		{
			const uint ModeValue = select(MaterialFlags.bNoDerivativeOps, 2, 1);
			Result = (IntToColor(ModeValue) * 0.75 + 0.25) * (IntToColor(TriIndex).x * 0.5 + 0.5);
		}
		else if (VisualizeMode == NANITE_VISUALIZE_FAST_CLEAR_TILES)
		{
			const uint TileData = FastClearTileVis[PixelPos];
			// Tile data has a counter for each MRT that is still marked as "clear" in
			// the meta data, indicating that the next fast clear eliminate will fully
			// write through this memory (slow tile).
			Result = ColorMapInferno(float(TileData) / float(GetShadingExportCount()));
		}
		else if (VisualizeMode == NANITE_VISUALIZE_NANITE_MASK)
		{
			Result = float3(0, 1, 0);
			Opacity = 0.5f;
		}
		else if (VisualizeMode == NANITE_VISUALIZE_EVALUATE_WORLD_POSITION_OFFSET)
		{
			const bool bAlwaysEvaluateWPO = (PrimitiveData.Flags & PRIMITIVE_SCENE_DATA_FLAG_HAS_ALWAYS_EVALUATE_WPO_MATERIALS) != 0u;
			const bool bEvaluateWPO = !bIsImposter && bWPOEnabled;
			if (bAlwaysEvaluateWPO)
			{
				Result = float3(1, 1, 0);
			}
			else if (bEvaluateWPO)
			{
				Result = float3(0, 1, 0);
			}
			else
			{
				Result = float3(1, 0, 0);
			}
		}
		else if (VisualizeMode == NANITE_VISUALIZE_PIXEL_PROGRAMMABLE_RASTER)
		{
			bool bHighlight = false;
			switch (GetPixelProgrammableVisMode())
			{
			case NANITE_PIXEL_PROG_VIS_MODE_DEFAULT:
				bHighlight = MaterialFlags.bPixelDiscard || MaterialFlags.bPixelDepthOffset;
				break;
			case NANITE_PIXEL_PROG_VIS_MODE_MASKED_ONLY:
				bHighlight = MaterialFlags.bPixelDiscard;
				break;
			case NANITE_PIXEL_PROG_VIS_MODE_PDO_ONLY:
				bHighlight = MaterialFlags.bPixelDepthOffset;
				break;
			default:
				break;
			}

			if (bFallbackRaster &&
				(PrimitiveData.PixelProgrammableDistanceSquared > 0.0f ||
				 PrimitiveData.MaterialDisplacementFadeOutSize > 0.0f))
			{
				// We've disabled pixel programmable for this cluster
				bHighlight = false;
			}

			Result = select(bHighlight, float3(1, 0, 0), float3(0.1, 0, 0.3));
		}
		else if (VisualizeMode == NANITE_VISUALIZE_LIGHTMAP_UVS)
		{
			const float2 LightmapUVs = AttributeData.TexCoords[PrimitiveData.LightmapUVIndex].Value;
			Result = float3(LightmapUVs.x, LightmapUVs.y, 0);
		}
		else if (VisualizeMode == NANITE_VISUALIZE_LIGHTMAP_UV_INDEX)
		{
			Result = IntToColor(PrimitiveData.LightmapUVIndex);
		}
		else if (VisualizeMode == NANITE_VISUALIZE_LIGHTMAP_DATA_INDEX)
		{
			Result = IntToColor(PrimitiveData.LightmapDataIndex);
		}
		else if (VisualizeMode == NANITE_VISUALIZE_POSITION_BITS)
		{
			const uint NumBits = Cluster.PosBits.x + Cluster.PosBits.y + Cluster.PosBits.z;

			if (NumBits <= 30)
			{
				Result = lerp(float3(0.0f, 1.0f, 0.0f), float3(1.0f, 1.0f, 1.0f), NumBits / 30.0f);
			}
			else
			{
				Result = lerp(float3(1.0f, 1.0f, 1.0f), float3(1.0f, 0.0f, 0.0f), (NumBits - 30) / (float)(3 * 16 - 30));
			}
		}
		else if (VisualizeMode == NANITE_VISUALIZE_VSM_STATIC_CACHING)
		{
			// TODO: We need to figure out a way to try and better visualize shadow clipmap WPO distances
			bool bAllowWPO = VirtualShadowMapIsWPOAllowed(PrimitiveData, -1);
			if (ShouldCacheInstanceAsStatic(VisibleCluster.InstanceId, false, bAllowWPO))
			{
				Result = float3(1, 0, 0);	
			}
			else
			{
				Result = float3(0, 0, 1);
			}
		}
		else if (VisualizeMode == NANITE_VISUALIZE_DISPLACEMENT_SCALE)
		{
			const float DisplacementCenter = RasterBinMeta[RasterBin].MaterialDisplacementParams.Center;
			const float DisplacementMagnitude = RasterBinMeta[RasterBin].MaterialDisplacementParams.Magnitude;

			const float MinDisplacement = (0.0f - DisplacementCenter) * DisplacementMagnitude;
			const float MaxDisplacement = (1.0f - DisplacementCenter) * DisplacementMagnitude;

			const float AbsDelta = abs(MaxDisplacement - MinDisplacement);

			Result = GreenToRedTurbo(AbsDelta / 30.0f);
		}
		else if (VisualizeMode == NANITE_VISUALIZE_VERTEX_COLOR)
		{
#if USE_EDITOR_SHADERS
			if (GetMeshPaintingShowMode() == NANITE_MESH_PAINTING_SHOW_SELECTED && !IsInstanceSelected(InstanceData, VisibleCluster, TriIndex))
			{
				Opacity = 0;
			}
#endif
			Result =  LinearToPostTonemapSpace(ApplyMeshPaintingChannelMode(GetMeshPaintingChannelMode(), AttributeData.VertexColor.Value));
		}
		else if (VisualizeMode == NANITE_VISUALIZE_MESH_PAINT_TEXTURE)
		{
#if USE_EDITOR_SHADERS
			if (GetMeshPaintingShowMode() == NANITE_MESH_PAINTING_SHOW_SELECTED && !IsInstanceSelected(InstanceData, VisibleCluster, TriIndex))
			{
				Opacity = 0;
			}
#endif
			if (GetMeshPaintingShowMode() == NANITE_MESH_PAINTING_SHOW_ALL && GetMeshPaintingTextureMode() == NANITE_MESH_PAINTING_TEXTURE_DEFAULT && !GetMeshPaintTextureDescriptorIsValid(PrimitiveData))
			{
				Opacity = 0;
			}
			
			if (Opacity > 0)
			{
				if (GetMeshPaintingTextureMode() == NANITE_MESH_PAINTING_TEXTURE_DEFAULT)
				{
					// Sample the shared mesh paint virtual texture atlas.
					TDual<float2> UV = GetTexCoord(Cluster, TriIndices, Barycentrics, GetMeshPaintTextureCoordinateIndex(PrimitiveData));
					float2 SVPositionXY = (float2)PixelPos + 0.5f;
					FVirtualTextureFeedbackParams VirtualTextureFeedback;
					InitializeVirtualTextureFeedback(VirtualTextureFeedback, SVPositionXY, View.FrameNumber);
					VTPageTableResult PageTableResult = TextureLoadVirtualPageTableGrad(Scene.MeshPaint.PageTableTexture, VTPageTableUniform_Unpack(GetMeshPaintTextureDescriptor(PrimitiveData)), UV.Value, VTADDRESSMODE_CLAMP, VTADDRESSMODE_CLAMP, UV.Value_dx, UV.Value_dy, SVPositionXY, 0, VirtualTextureFeedback);
					float4 Color = TextureVirtualSample(Scene.MeshPaint.PhysicalTexture, View.SharedBilinearClampedSampler, PageTableResult, 0, VTUniform_Unpack(Scene.MeshPaint.PackedUniform));
					FinalizeVirtualTextureFeedback(VirtualTextureFeedback, float4(SVPositionXY, 0, 0), 1.0f, View.FrameNumber, View.VTFeedbackBuffer);
					Result = LinearToPostTonemapSpace(ApplyMeshPaintingChannelMode(GetMeshPaintingChannelMode(), Color));
				}
				else
				{
					// Sample a single selected mesh paint texture.
					TDual<float2> UV = GetTexCoord(Cluster, TriIndices, Barycentrics, MeshPaintTextureCoordinate);
					float4 Color = MeshPaintTexture.SampleGrad(View.SharedBilinearClampedSampler, UV.Value, UV.Value_dx, UV.Value_dy);
					Result = LinearToPostTonemapSpace(ApplyMeshPaintingChannelMode(GetMeshPaintingChannelMode(), Color));
				}
			}
		}
		else if (VisualizeMode == NANITE_VISUALIZE_PICKING)
		{
			bApplySobel = false;

			const UlongType PickedVisPixel = VisBuffer64[PickingPos];

			uint PickedDepthInt;
			uint PickedVisibleClusterIndex;
			uint PickedTriIndex;
			UnpackVisPixel(PickedVisPixel, PickedDepthInt, PickedVisibleClusterIndex, PickedTriIndex);

			FVisibleCluster PickedVisibleCluster	= GetVisibleCluster(PickedVisibleClusterIndex);
			FInstanceSceneData PickedInstanceData	= GetInstanceSceneData(PickedVisibleCluster, false);

			bool bPicked = false;
			switch (GetPickingDomain())
			{
				case NANITE_PICKING_DOMAIN_TRIANGLE:
					bPicked = (VisibleClusterIndex == PickedVisibleClusterIndex && PickedTriIndex == TriIndex);
					Result = IntToColor(TriIndex) * 0.8 + 0.2;
					break;

				case NANITE_PICKING_DOMAIN_CLUSTER:
					bPicked = (VisibleClusterIndex == PickedVisibleClusterIndex);
					Result = IntToColor(VisibleCluster.ClusterIndex) * 0.8;
					break;

				case NANITE_PICKING_DOMAIN_INSTANCE:
					bPicked = (VisibleCluster.InstanceId == PickedVisibleCluster.InstanceId);
					Result = IntToColor(VisibleCluster.InstanceId) * 0.8;
					break;

				case NANITE_PICKING_DOMAIN_PRIMITIVE:
					bPicked = (InstanceData.PrimitiveId == PickedInstanceData.PrimitiveId);
					Result = IntToColor(InstanceData.PrimitiveId) * 0.8;
					break;

				default:
					// Invalid picking domain
					break;
			}

			if (bPicked)
			{
				Result = float3(1.0f, 0.0f, 1.0f);
			}
			else
			{
				Result *= 0.3f;
			}
		}
	}
	else
	{
		// Non-Nanite Pixel

		if (GetVisualizeMode() == NANITE_VISUALIZE_NANITE_MASK)
		{
			if (SceneDepth[PixelPos] > 0.0f) // only visualize written fragments
			{
				Result = float3(1, 0, 0);
				Opacity = 0.5f;
			}
		}
		else if (GetVisualizeMode() == NANITE_VISUALIZE_VERTEX_COLOR || GetVisualizeMode() == NANITE_VISUALIZE_MESH_PAINT_TEXTURE)
		{
			if (GetMeshPaintingShowMode() == NANITE_MESH_PAINTING_SHOW_SELECTED)
			{
				Opacity = 0;
			}
		}
	}

	if (bApplySobel && ShouldApplySobelFilter() && (!GetCompositeWithSceneDepth() || UnpackedMask.bIsNanitePixel))
	{
		Result = ApplySobelFilter(PixelPos, DepthInt, Result, OutlineColor, bDarkOutline);
	}

	DebugOutput[PixelPos] = float4(Result, Opacity);
}

// Axis 0...5 == -X, +X, -Y, +Y, -Z, +Z 
void SetMinMaxValueByAxis(float Value, uint Axis, inout float3 MinPoint, inout float3 MaxPoint)
{
	switch(Axis)
	{
	case 0: MinPoint.x = Value; break;
	case 1: MaxPoint.x = Value; break;
	case 2: MinPoint.y = Value; break;
	case 3: MaxPoint.y = Value; break;
	case 4: MinPoint.z = Value; break;
	case 5: MaxPoint.z = Value; break;
	default: break;
	}
}

float GetMinMaxValueByAxis(uint Axis, float3 MinPoint, float3 MaxPoint)
{
	switch(Axis)
	{
	case 0: return MinPoint.x;
	case 1: return MaxPoint.x;
	case 2: return MinPoint.y;
	case 3: return MaxPoint.y;
	case 4: return MinPoint.z;
    case 5: return MaxPoint.z;
	default: return 0.f;
	}
}



[numthreads(1, 1, 1)]
void PickingCS(uint3 DTID : SV_DispatchThreadID, uint3 GID : SV_GroupID)
{
	FNanitePickingFeedback FeedbackResults = (FNanitePickingFeedback)0;

	const uint2 PickingPos	= uint2(PickingPixelPos);
	FeedbackResults.PixelX = PickingPos.x;
	FeedbackResults.PixelY = PickingPos.y;

	const UlongType PickedPixel = VisBuffer64[PickingPos];

	uint DepthInt;
	uint VisibleClusterIndex;
	uint TriIndex;
	UnpackVisPixel(PickedPixel, DepthInt, VisibleClusterIndex, TriIndex);

	FNaniteView NaniteView = GetNaniteView(0);

	FShadingMask UnpackedMask = UnpackShadingMask(ShadingMask[PickingPos]);

	if (VisibleClusterIndex != 0xFFFFFFFFu && UnpackedMask.bIsNanitePixel)
	{
		UlongType DbgPixel = DbgBuffer64[PickingPos];
		uint DebugDepthInt;
		uint DebugValueMax;
		UnpackDbgPixel(DbgPixel, DebugDepthInt, DebugValueMax);
		uint DebugValueAdd = DbgBuffer32[PickingPos];

		const uint PackedMaterialFlags = ShadingBinData.Load<FNaniteShadingBinMeta>(UnpackedMask.ShadingBin * NANITE_SHADING_BIN_META_BYTES).MaterialFlags;
		FNaniteMaterialFlags MaterialFlags = UnpackNaniteMaterialFlags(PackedMaterialFlags);

		FVisibleCluster VisibleCluster				= GetVisibleCluster(VisibleClusterIndex);
		FInstanceSceneData InstanceData				= GetInstanceSceneData(VisibleCluster, false);
		FInstanceDynamicData InstanceDynamicData	= CalculateInstanceDynamicData(NaniteView, InstanceData);
		FPrimitiveSceneData PrimitiveData			= GetPrimitiveData(InstanceData.PrimitiveId);
		FCluster Cluster							= GetCluster(VisibleCluster.PageIndex, VisibleCluster.ClusterIndex);

		const bool bWPOEnabled = (VisibleCluster.Flags & NANITE_CULLING_FLAG_ENABLE_WPO) != 0;
		const bool bFallbackRaster = (VisibleCluster.Flags & NANITE_CULLING_FLAG_FALLBACK_RASTER) != 0;

		uint RasterBin = GetMaterialRasterBin(Cluster, InstanceData.PrimitiveId, MeshPassIndex, TriIndex, RegularMaterialRasterBinCount, bFallbackRaster);
		RasterBin = RemapRasterBin(RasterBin, RenderFlags, MaterialFlags);

		FeedbackResults.PrimitiveId			= InstanceData.PrimitiveId;
		FeedbackResults.InstanceId			= VisibleCluster.InstanceId;
		FeedbackResults.PersistentIndex		= PrimitiveData.PersistentPrimitiveIndex;
		FeedbackResults.ClusterIndex		= VisibleCluster.ClusterIndex;
		FeedbackResults.GroupIndex			= Cluster.GroupIndex;
		FeedbackResults.PageIndex			= VisibleCluster.PageIndex;
		FeedbackResults.TriangleIndex		= TriIndex;
		FeedbackResults.DepthInt			= DepthInt;
		FeedbackResults.RasterMode			= DebugValueMax;
		FeedbackResults.RasterBin			= RasterBin;
		FeedbackResults.ShadingBin			= GetMaterialShadingBin(Cluster, InstanceData.PrimitiveId, MeshPassIndex, TriIndex);
		FeedbackResults.MaterialIndex		= GetRelativeMaterialIndex(Cluster, TriIndex);
		FeedbackResults.MaterialCount		= GetMaterialCount(Cluster);
		FeedbackResults.MaterialMode		= IsMaterialFastPath(Cluster) ? 0u : 1u;
		FeedbackResults.HierarchyOffset		= InstanceData.NaniteHierarchyOffset;
		FeedbackResults.RuntimeResourceID	= InstanceData.NaniteRuntimeResourceID;

		const float4 ApproximateClusterBoundsColor	= ColorCyan;
		const float4 SkinnedClusterBoundsColor		= ColorLightGreen;
		const float4 ClusterBoundsColor				= ColorLightRed;
		const float4 InstanceBoundsColor			= ColorOrange;
		const float4 ClusterLODBoundsColor			= ColorYellow;

		FShaderPrintContext ShaderPrint = InitShaderPrintContext(true, float2(0.15, 0.1));
		const float4x4 LocalToTranslatedWorld = DFFastToTranslatedWorld(InstanceData.LocalToWorld, NaniteView.PreViewTranslation);

		const float3 WPOExtent = GetLocalMaxWPOExtent(PrimitiveData, InstanceData, bWPOEnabled);
		const float DisplacementExtent = GetMaxMaterialDisplacementExtent(PrimitiveData, bFallbackRaster, false /* bIsShadowPass */);

		// If WPO and/or material displacement is enabled, the cluster bounds will have been dilated to account for max displacement
		float3 ClusterBoxBoundsCenter = Cluster.BoxBoundsCenter;
		float3 ClusterBoxBoundsExtent = Cluster.BoxBoundsExtent;
		float4 ClusterLODSphere = Cluster.LODBounds;

		// Pre-Skinned Bounds
		AddOBBTWS(
			ShaderPrint,
			ClusterBoxBoundsCenter - ClusterBoxBoundsExtent,
			ClusterBoxBoundsCenter + ClusterBoxBoundsExtent,
			ClusterBoundsColor,
			LocalToTranslatedWorld
		);

		const bool bIsSkinned = (PrimitiveData.Flags & PRIMITIVE_SCENE_DATA_FLAG_SKINNED_MESH) != 0 && Cluster.bSkinning;

		FNaniteSkinningHeader SkinningHeader = (FNaniteSkinningHeader)0;
		BRANCH
		if (bIsSkinned)
		{
			SkinningHeader = LoadNaniteSkinningHeader(InstanceData.PrimitiveId);
			FBoneInfluenceHeader BoneInfluenceHeader = GetBoneInfluenceHeader(Cluster);

			float3 MinBounds = float( 999999999.0f).xxx;
			float3 MaxBounds = float(-999999999.0f).xxx;

			for (uint VertexID = 0; VertexID < Cluster.NumVerts; ++VertexID)
			{
				float3 PointLocal = DecodePosition(VertexID, Cluster);
				float3 PointSkinned = float3(0.0f, 0.0f, 0.0f);

				LOOP
				for (uint InfluenceIndex = 0; InfluenceIndex < BoneInfluenceHeader.NumVertexBoneInfluences; ++InfluenceIndex)
				{
					uint BoneIndex = 0;
					float BoneWeight = 0.0f;
					DecodeVertexBoneInfluence(BoneInfluenceHeader, VertexID, InfluenceIndex, BoneIndex, BoneWeight);

					float4x3 CurrentBoneTransform = LoadNaniteBoneTransform(SkinningHeader.TransformBufferOffset + InstanceData.SkinningData + BoneIndex);
					PointSkinned += mul(float4(PointLocal, 1.0f), CurrentBoneTransform) * BoneWeight;
				}

				MinBounds = min(MinBounds, PointSkinned);
				MaxBounds = max(MaxBounds, PointSkinned);
			}

			ClusterBoxBoundsCenter = (MaxBounds + MinBounds) * 0.5f;
			ClusterBoxBoundsExtent = (MaxBounds - MinBounds) * 0.5f;
		}

		BRANCH
		if ((PrimitiveData.Flags & PRIMITIVE_SCENE_DATA_FLAG_SPLINE_MESH) != 0 &&
			(InstanceData.Flags & INSTANCE_SCENE_DATA_FLAG_HAS_PAYLOAD_EXTENSION) != 0)
		{
			// Only show the spline mesh debug stuff when picking clusters
			ShaderPrint.bIsActive = GetPickingDomain() == NANITE_PICKING_DOMAIN_CLUSTER;

			const FSplineMeshShaderParams SplineMeshParams = SplineMeshLoadParamsFromInstancePayload(InstanceData);
			const FSplineMeshDeformedLocalBounds NewBounds = SplineMeshDeformLocalBoundsDebug(
				SplineMeshParams,
				ShaderPrint,
				LocalToTranslatedWorld,
				ClusterBoxBoundsCenter,
				ClusterBoxBoundsExtent
			);
			ClusterBoxBoundsCenter = NewBounds.BoundsCenter;
			ClusterBoxBoundsExtent = NewBounds.BoundsExtent;

			ClusterLODSphere = SplineMeshDeformLODSphereBounds(SplineMeshParams, ClusterLODSphere);

			ShaderPrint.bIsActive = true;
		}
		ClusterBoxBoundsExtent += WPOExtent + DisplacementExtent.xxx;

		// Skinned Bounds
		AddOBBTWS(
			ShaderPrint,
			ClusterBoxBoundsCenter - ClusterBoxBoundsExtent,
			ClusterBoxBoundsCenter + ClusterBoxBoundsExtent,
			SkinnedClusterBoundsColor,
			LocalToTranslatedWorld
		);

		// Estimated Skinned Bounds
		BRANCH
		if (bIsSkinned)
		{
			SkinningHeader = LoadNaniteSkinningHeader(InstanceData.PrimitiveId);
			FBoneInfluenceHeader BoneInfluenceHeader = GetBoneInfluenceHeader(Cluster);

			float3 MinBounds = float( 999999999.0f).xxx;
			float3 MaxBounds = float(-999999999.0f).xxx;
			
			// Operate on max of NANITE_MAX_CLUSTER_BONE_INFLUENCES bones for now. This computation will be moved to the cluster generation/encoding on the CPU side.
			const uint MaxClusterBoneCount = NANITE_MAX_CLUSTER_BONE_INFLUENCES;

			const uint IBI = 0xFFFFFFFFu;
			uint ClusterBoneIndexes[MaxClusterBoneCount] = {IBI, IBI, IBI, IBI, IBI, IBI, IBI, IBI, IBI, IBI, IBI, IBI, IBI, IBI, IBI, IBI};
			float ClusterBoneWeightMin[MaxClusterBoneCount];
			float ClusterBoneWeightMax[MaxClusterBoneCount];
			float3 ClusterBoneBoundsMin[MaxClusterBoneCount];
			float3 ClusterBoneBoundsMax[MaxClusterBoneCount];

			UNROLL
			for (uint BoneIndex = 0; BoneIndex < MaxClusterBoneCount; ++BoneIndex)
			{
				ClusterBoneWeightMin[BoneIndex] = 0.0f;
				ClusterBoneWeightMax[BoneIndex] = 0.0f;
				ClusterBoneBoundsMin[BoneIndex] = MinBounds;
				ClusterBoneBoundsMax[BoneIndex] = MaxBounds;
			}

		#if 1
			// Load cooked data
			for (uint i = 0; i < BoneInfluenceHeader.NumClusterBoneInfluences; ++i)
			{
				FClusterBoneInfluence BoneInfluence = DecodeClusterBoneInfluence(Cluster, BoneInfluenceHeader, i);
				ClusterBoneIndexes[i]	= BoneInfluence.BoneIndex;
				ClusterBoneWeightMin[i] = BoneInfluence.MinWeight;
				ClusterBoneWeightMax[i] = BoneInfluence.MaxWeight;
				ClusterBoneBoundsMin[i] = BoneInfluence.BoundMin;
				ClusterBoneBoundsMax[i] = BoneInfluence.BoundMax;
			}
		#else
			for (uint VertexID = 0; VertexID < Cluster.NumVerts; ++VertexID)
			{
				float3 PointLocal = DecodePosition(VertexID, Cluster);
				
				LOOP
				for (uint InfluenceIndex = 0; InfluenceIndex < BoneInfluenceHeader.NumVertexBoneInfluences; ++InfluenceIndex)
				{
					uint BoneIndex = 0;
					float BoneWeight = 0.0f;
					DecodeVertexBoneInfluence(BoneInfluenceHeader, VertexID, InfluenceIndex, BoneIndex, BoneWeight);

					// Have we reached the end of weights?
					if (BoneWeight == 0.0f)
					{
						break;
					}
					// Have we seen this bone index already?
					for (uint ClusterBoneIndex = 0; ClusterBoneIndex < NANITE_MAX_CLUSTER_BONE_INFLUENCES; ++ClusterBoneIndex)
					{
						if (ClusterBoneIndexes[ClusterBoneIndex] == BoneIndex)
						{
							ClusterBoneWeightMin[ClusterBoneIndex] = min(BoneWeight, ClusterBoneWeightMin[ClusterBoneIndex]);
							ClusterBoneWeightMax[ClusterBoneIndex] = max(BoneWeight, ClusterBoneWeightMax[ClusterBoneIndex]);
							
							ClusterBoneBoundsMin[ClusterBoneIndex] = min(PointLocal, ClusterBoneBoundsMin[ClusterBoneIndex]);
							ClusterBoneBoundsMax[ClusterBoneIndex] = max(PointLocal, ClusterBoneBoundsMax[ClusterBoneIndex]);
							break;
						}
						if (ClusterBoneIndexes[ClusterBoneIndex] == IBI)
						{
							ClusterBoneIndexes[ClusterBoneIndex] = BoneIndex;
							ClusterBoneWeightMin[ClusterBoneIndex] = BoneWeight;
							ClusterBoneWeightMax[ClusterBoneIndex] = BoneWeight;
							ClusterBoneBoundsMin[ClusterBoneIndex] = PointLocal;
							ClusterBoneBoundsMax[ClusterBoneIndex] = PointLocal;
							break;
						}
					}
				}
			}
		#endif

			// Deform each of the cluster bone bboxes by their associated bone transform.
			uint ClusterBoneCount = 0;
			for (uint ClusterBoneIndex = 0; ClusterBoneIndex < NANITE_MAX_CLUSTER_BONE_INFLUENCES; ++ClusterBoneIndex)
			{
				if (ClusterBoneIndexes[ClusterBoneIndex] == IBI)
				{
					break;
				}
				ClusterBoneCount++;

				float3 BoneBoundsMin = ClusterBoneBoundsMin[ClusterBoneIndex];
				float3 BoneBoundsMax = ClusterBoneBoundsMax[ClusterBoneIndex];
				
				float3 BoneBoundsCorners[8] = {
					float3(BoneBoundsMin.x, BoneBoundsMin.y, BoneBoundsMin.z),
					float3(BoneBoundsMax.x, BoneBoundsMin.y, BoneBoundsMin.z),
					float3(BoneBoundsMin.x, BoneBoundsMax.y, BoneBoundsMin.z),
					float3(BoneBoundsMax.x, BoneBoundsMax.y, BoneBoundsMin.z),
					float3(BoneBoundsMin.x, BoneBoundsMin.y, BoneBoundsMax.z),
					float3(BoneBoundsMax.x, BoneBoundsMin.y, BoneBoundsMax.z),
					float3(BoneBoundsMin.x, BoneBoundsMax.y, BoneBoundsMax.z),
					float3(BoneBoundsMax.x, BoneBoundsMax.y, BoneBoundsMax.z)
				};

				uint BoneIndex = ClusterBoneIndexes[ClusterBoneIndex];
				if (BoneIndex == IBI)
				{
					break;
				}
				float4x3 BoneTransform = LoadNaniteBoneTransform(SkinningHeader.TransformBufferOffset + InstanceData.SkinningData + BoneIndex);
				ClusterBoneBoundsMin[ClusterBoneIndex] = float(999999999.0f).xxx;
				ClusterBoneBoundsMax[ClusterBoneIndex] = float(-999999999.0f).xxx;

				for (uint CornerIndex = 0; CornerIndex < 8; ++CornerIndex)
				{
					float3 SkinnedClusterBoxCorner = mul(float4(BoneBoundsCorners[CornerIndex], 1.0f), BoneTransform);
						
					ClusterBoneBoundsMin[ClusterBoneIndex] = min(ClusterBoneBoundsMin[ClusterBoneIndex], SkinnedClusterBoxCorner);
					ClusterBoneBoundsMax[ClusterBoneIndex] = max(ClusterBoneBoundsMax[ClusterBoneIndex], SkinnedClusterBoxCorner);
				}
			}

			for (uint Axis = 0; Axis < 6; ++Axis)
			{
				// Find the extrema and second extrema bone values for this axis based on their deformed bbox.
				uint ExtremaIndex = IBI;
				uint ExtremaIndex2 = IBI;
				float ExtremaValue = (Axis % 2 == 0) ? 999999999.0f : -999999999.0f;
				float ExtremaValue2 = (Axis % 2 == 0) ? 999999999.0f : -999999999.0f;

#if 0
				ShaderPrint.StartPos.x = 0.15 + 0.12 * Axis;
				ShaderPrint.StartPos.y = 0.1;
				ShaderPrint.Pos = ShaderPrint.StartPos;
				switch(Axis)
				{
				case 0:
					Print(ShaderPrint, TEXT("Axis: -X"), FontCyan);
					break;
				case 1:
					Print(ShaderPrint, TEXT("Axis: +X"), FontCyan);
					break;
				case 2:
					Print(ShaderPrint, TEXT("Axis: -Y"), FontCyan);
					break;
				case 3:
					Print(ShaderPrint, TEXT("Axis: +Y"), FontCyan);
					break;
				case 4:
					Print(ShaderPrint, TEXT("Axis: -Z"), FontCyan);
					break;
				case 5:
					Print(ShaderPrint, TEXT("Axis: +Z"), FontCyan);
					break;
				}
				Newline(ShaderPrint);
#endif
				
				for (uint ClusterBoneIndex = 0; ClusterBoneIndex < NANITE_MAX_CLUSTER_BONE_INFLUENCES; ++ClusterBoneIndex)
				{
					if (ClusterBoneIndexes[ClusterBoneIndex] == IBI)
					{
						break;
					}
					
					float BBoxValue = GetMinMaxValueByAxis(Axis, ClusterBoneBoundsMin[ClusterBoneIndex], ClusterBoneBoundsMax[ClusterBoneIndex]);

					if (Axis % 2 == 0)		// Negative
					{
						if (BBoxValue < ExtremaValue)
						{
							if (ExtremaValue < ExtremaValue2)
							{
								ExtremaIndex2 = ExtremaIndex;
								ExtremaValue2 = ExtremaValue;
							}
							
							ExtremaIndex = ClusterBoneIndex;
							ExtremaValue = BBoxValue;
						}
						else if (BBoxValue < ExtremaValue2)
						{
							ExtremaIndex2 = ClusterBoneIndex;
							ExtremaValue2 = BBoxValue;
						}
					}
					else
					{
						if (BBoxValue > ExtremaValue)
						{
							if (ExtremaValue > ExtremaValue2)
							{
								ExtremaIndex2 = ExtremaIndex;
								ExtremaValue2 = ExtremaValue;
							}
							
							ExtremaIndex = ClusterBoneIndex;
							ExtremaValue = BBoxValue;
						}
						else if (BBoxValue > ExtremaValue2)
						{
							ExtremaIndex2 = ClusterBoneIndex;
							ExtremaValue2 = BBoxValue;
						}
					}
				}

				float SkinnedValue = ClusterBoneWeightMax[ExtremaIndex] * ExtremaValue;

				if (ExtremaIndex2 != IBI)
				{
					float Weight2 = 1.0f - ClusterBoneWeightMax[ExtremaIndex];
					for (uint ClusterBoneIndex = 0; ClusterBoneIndex < NANITE_MAX_CLUSTER_BONE_INFLUENCES; ++ClusterBoneIndex)
					{
						if (ClusterBoneIndexes[ClusterBoneIndex] == IBI)
						{
							break;
						}
						if (ClusterBoneIndex != ExtremaIndex && ClusterBoneIndex != ExtremaIndex2)
						{
							float MinWeight = ClusterBoneWeightMin[ClusterBoneIndex];
							Weight2 -= MinWeight;

							SkinnedValue += MinWeight * GetMinMaxValueByAxis(Axis, ClusterBoneBoundsMin[ClusterBoneIndex], ClusterBoneBoundsMax[ClusterBoneIndex]); 
						}
					}
					SkinnedValue += Weight2 * ExtremaValue2;
				}

#if 0
				Print(ShaderPrint, TEXT("Index 1: "), FontCyan);
				Print(ShaderPrint, ExtremaIndex, FontCyan);
				Newline(ShaderPrint);
				Print(ShaderPrint, TEXT("BBox 1: "), FontCyan);
				Print(ShaderPrint, ExtremaValue, FontCyan);
				Newline(ShaderPrint);
				Print(ShaderPrint, TEXT("MaxWeight 1: "), FontCyan);
				Print(ShaderPrint, ClusterBoneWeightMax[ExtremaIndex], FontCyan);
				Newline(ShaderPrint);
				Print(ShaderPrint, TEXT("MinWeight 1: "), FontCyan);
				Print(ShaderPrint, ClusterBoneWeightMin[ExtremaIndex], FontCyan);
				Newline(ShaderPrint);
				
				
				Print(ShaderPrint, TEXT("Index 2: "), FontCyan);
				Print(ShaderPrint, ExtremaIndex2, FontCyan);
				Newline(ShaderPrint);
				Print(ShaderPrint, TEXT("BBox 2: "), FontCyan);
				Print(ShaderPrint, ExtremaValue2, FontCyan);
				Newline(ShaderPrint);
				Print(ShaderPrint, TEXT("MaxWeight 2: "), FontCyan);
				Print(ShaderPrint, ClusterBoneWeightMax[ExtremaIndex2], FontCyan);
				Newline(ShaderPrint);
				Print(ShaderPrint, TEXT("MinWeight 2: "), FontCyan);
				Print(ShaderPrint, ClusterBoneWeightMin[ExtremaIndex2], FontCyan);
				Newline(ShaderPrint);
#endif

				SetMinMaxValueByAxis(SkinnedValue, Axis, MinBounds, MaxBounds);
			}

#if 0
			Print(ShaderPrint, TEXT("Cluster Bone Count: "), FontGreen);
			Print(ShaderPrint, ClusterBoneCount, FontGreen);
			Newline(ShaderPrint);
#endif

			ClusterBoxBoundsCenter = (MaxBounds + MinBounds) * 0.5f;
			ClusterBoxBoundsExtent = (MaxBounds - MinBounds) * 0.5f;
			
			AddOBBTWS(
				ShaderPrint,
				ClusterBoxBoundsCenter - ClusterBoxBoundsExtent,
				ClusterBoxBoundsCenter + ClusterBoxBoundsExtent,
				ApproximateClusterBoundsColor,
				LocalToTranslatedWorld
			);
		}

		AddReferentialTWS(ShaderPrint, LocalToTranslatedWorld, 50.f);

		// Only show the cluster LOD bounds with cluster picking to reduce visual noise
		if (GetPickingDomain() == NANITE_PICKING_DOMAIN_CLUSTER)
		{
			AddSphereTWS(
				ShaderPrint,
				mul(float4(ClusterLODSphere.xyz, 1), LocalToTranslatedWorld).xyz,
				ClusterLODSphere.w * InstanceData.NonUniformScale.w,
				ClusterLODBoundsColor,
				32
			);
		}
	}
	else
	{
		FeedbackResults.PrimitiveId = INVALID_PRIMITIVE_ID;
	}

	FeedbackBuffer[0] = FeedbackResults;
}